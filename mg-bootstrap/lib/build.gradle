plugins {

    // Backport of Java 8 closures.
    id "me.tatarka.retrolambda" version "3.1.0"
}

// This is a library module.
apply plugin: 'com.android.library'

ext {
    PUBLISH_GROUP_ID = 'com.miguelgaeta.bootstrap'
    PUBLISH_ARTIFACT_ID = 'mg-bootstrap'
    PUBLISH_VERSION = '2.7.0'
}

allprojects {

    tasks.withType(Javadoc) {

        // Disable javadoc lint errors.
        options.addStringOption('Xdoclint:none', '-quiet')

        // Don't generate at all.
        exclude '**/*.java'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 270
        versionName "2.7.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

//
// Fairly exhaustive list of all available libraries: http://bit.ly/1twylbS
//

dependencies {

    // Support libraries.
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'

    // Provides support for annotated get/set.
    compile 'org.projectlombok:lombok:1.16.4'

    // Custom fonts library.
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    // XML view injections.
    compile 'com.jakewharton:butterknife:6.1.0'

    // Used for push notifications.
    compile 'com.google.android.gms:play-services-gcm:7.5.0'

    // Rx Java (reactive programming)
    compile 'io.reactivex:rxjava:1.0.12'
    compile 'io.reactivex:rxandroid:0.24.0'

    // Google json library.
    compile 'com.google.code.gson:gson:2.3.1'

    // Library for making REST calls.
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    // Custom HTTP client that is better than the default.
    compile 'com.squareup.okhttp:okhttp:2.4.0'

    // Library for network image loading.
    compile 'com.facebook.fresco:fresco:0.5.2'
    compile 'com.facebook.fresco:imagepipeline-okhttp:0.5.2'

    // ISO time format handler
    compile 'net.danlew:android.joda:2.8.1'

    // Logging library.
    compile 'com.jakewharton.timber:timber:3.1.0'

    // Pusher
    compile 'org.java-websocket:java-websocket:1.3.1'
}

// Deployment script: https://github.com/blundell/release-android-library
apply from: 'build.release-aar.gradle'
