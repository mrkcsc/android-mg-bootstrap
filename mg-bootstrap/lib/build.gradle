plugins {

    // Backport of Java 8 closures.
    id "me.tatarka.retrolambda" version "3.2.3"
}

// This is a library module.
apply plugin: 'com.android.library'

ext {
    PUBLISH_GROUP_ID = 'com.miguelgaeta.bootstrap'
    PUBLISH_ARTIFACT_ID = 'mg-bootstrap'
    PUBLISH_VERSION = '6.2.0'
}

allprojects {

    tasks.withType(Javadoc) {

        // Disable javadoc lint errors.
        options.addStringOption('Xdoclint:none', '-quiet')

        // Don't generate at all.
        exclude '**/*.java'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 620
        versionName '6.2.0'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }
}

//
// Fairly exhaustive list of all available libraries: http://bit.ly/1twylbS
//

dependencies {

    // Support libraries.
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile "com.android.support:design:23.1.0"

    // Provides support for annotations.
    compile 'org.projectlombok:lombok:1.16.6'

    // Custom fonts library.
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    // XML view injections.
    compile 'com.jakewharton:butterknife:7.0.1'

    // Rx Java (reactive programming)
    compile 'io.reactivex:rxjava:1.0.15'
    compile 'io.reactivex:rxandroid:1.0.1'

    // Google json library.
    compile 'com.google.code.gson:gson:2.4'

    // Byte stream serializer.
    compile 'com.esotericsoftware:kryo:3.0.3'

    // Library for making REST calls.
    compile 'com.squareup.retrofit:retrofit:1.9.0' // TODO: 2.0.0-beta2

    // Custom HTTP client that is better than the default.
    compile 'com.squareup.okhttp:okhttp:2.5.0'

    // Library for network image loading.
    compile 'com.facebook.fresco:fresco:0.8.1'
    compile 'com.facebook.fresco:imagepipeline-okhttp:0.8.1'

    // Image cropper.
    compile 'me.villani.lorenzo.android:android-cropimage:1.1.0'

    // ISO time format handler
    compile 'net.danlew:android.joda:2.9.0'

    // Logging library.
    compile 'com.jakewharton.timber:timber:4.1.0'

    // Pusher
    compile 'org.java-websocket:java-websocket:1.3.1'

    // Streaming API for android.
    compile 'com.annimon:stream:1.0.4'
}

// Deployment script: https://github.com/blundell/release-android-library
apply from: 'build.release-aar.gradle'
